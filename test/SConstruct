import os

env = Environment(
    ENV = os.environ,
    CXX='c++',
    CXXFLAGS= ['-g', '-Wall', '-Wextra',
               '-pedantic', '-std=c++11',
                '-fdiagnostics-color=always',
               '-I/usr/local/include', '-I.'],
    CPPPATH='..',
    LINKFLAGS='-L/usr/local/lib')

# allows highighting to print to terminal from compiler output
env['ENV']['TERM'] = os.environ['TERM']

progs = Split(
    '''
    accumulate
    chain
    combinations
    combinations_with_replacement
    compress
    count
    cycle
    dropwhile
    enumerate
    filter
    filterfalse
    groupby
    grouper
    imap
    permutations
    powerset
    product
    range
    repeat
    reversed
    slice
    sliding_window
    sorted
    takewhile
    unique_everseen
    unique_justseen
    zip

    iteratoriterator
    mixed
    helpers
    '''
)

conf = Configure(env)

# if catch isn't available, exit
if not conf.CheckCXXHeader('catch.hpp'):
    print("WARNING: catch.hpp not found, run ./download_catch.sh first")
    Exit(1)

if conf.CheckCXXHeader('boost/optional.hpp'):
    progs.append('zip_longest')

env = conf.Finish()

test_sources = ['test_{}.cpp'.format(p) for p in progs]

for test_src in test_sources:
    env.Program([test_src, 'test_main.cpp'])

env.Program('test_all', ['test_main.cpp'] + test_sources)
