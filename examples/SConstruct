import os

env = Environment(
    ENV = {'PATH' : os.environ['PATH']},
    CXX='c++',
    CXXFLAGS= ['-g', '-Wall', '-Wextra',
               '-pedantic', '-std=c++11',
                '-fdiagnostics-color=always',
               '-I/usr/local/include'],
    CPPPATH='..',
    LINKFLAGS='-L/usr/local/lib')

# allows highighting to print to terminal from compiler output
env['ENV']['TERM'] = os.environ['TERM']

progs = Split(
    '''
    accumulate
    chain
    combinatoric
    compress
    count
    cycle
    dropwhile
    enumerate
    filter
    filterfalse
    groupby
    grouper
    imap
    range
    repeat
    reversed
    slice
    sliding_window
    sorted
    takewhile
    unique_justseen
    unique_everseen
    zip
    ''')

if Configure(env).CheckCXXHeader('boost/optional.hpp'):
    progs.append('zip_longest')
    progs.append('mixed')

for p in progs:
    env.Program('{0}_examples.cpp'.format(p))
